plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.sgtsilvio.gradle.maven-central-publishing' version '0.4.1'
}

group = 'com.zebrunner'
version = "${version != 'unspecified' ? version : 'local'}"

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}


dependencies {
    compileOnly 'com.konghq:unirest-java:3.14.5'
    implementation 'org.yaml:snakeyaml:2.4'

    implementation 'net.bytebuddy:byte-buddy:1.12.18'
    compileOnly 'io.appium:java-client:8.3.0'

    implementation 'org.slf4j:slf4j-api:1.7.36'
    compileOnly 'log4j:log4j:1.2.17'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.17.2'
    compileOnly 'ch.qos.logback:logback-classic:1.5.17'

    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    compileOnly 'org.projectlombok:lombok:1.18.36'
}

tasks.jar {
    manifest {
        attributes(
                "Premain-Class": "com.zebrunner.agent.core.webdriver.DriverSessionsAgent",
                "Can-Redefine-Classes": false,
                "Can-Retransform-Classes": true,
                "Build-Jdk": "11"
        )
    }
}

apply from: 'publish-maven.gradle'
